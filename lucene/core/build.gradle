/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: org.apache.lucene.gradle.dist.PartOfDist

archivesBaseName = 'lucene-core'

def momanCommitHash="5c5c2a1e4dea"
def momanUrl = "https://bitbucket.org/jpbarrette/moman/get/${momanCommitHash}.zip"

dependencies {
  testImplementation project(':lucene:lucene-codecs')
  testImplementation project(':lucene:lucene-test-framework')
}

task downloadMoman(type: org.apache.lucene.gradle.Download) {
  sourceUrl = "${momanUrl}"
  target = mfile("${buildDir}", 'moman.zip')
}

task installMoman(){
  doLast {
    ant.unzip(src: filePath("${buildDir}/moman.zip"), dest: filePath("${buildDir}/moman"), overwrite:"true") {
      ant.cutdirsmapper(dirs: "1")
    }
  }
  dependsOn downloadMoman
}

task fixCRLFAutomaton(type: org.apache.lucene.gradle.FixCRLF) {
  sourceDir = "src/java/org/apache/lucene/util/automaton"
  includes="*ParametricDescription.java"
  encoding="UTF-8"
}

task createLevAutomaton {
  group = 'Build Regenerate'
  description = "Regenerates LevAutomaton src files."
  
  doLast {
    exec {
      workingDir 'src/java/org/apache/lucene/util/automaton'
      executable "${python_exe}"
      args = ['-B', 'createLevAutomata.py', '1', 'True', filePath("${buildDir}/moman/finenight/python")]
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/automaton'
      executable "${python_exe}"
      args = ['-B', 'createLevAutomata.py', '1', 'False', filePath("${buildDir}/moman/finenight/python")]
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/automaton'
      executable "${python_exe}"
      args = ['-B', 'createLevAutomata.py', '2', 'True', filePath("${buildDir}/moman/finenight/python")]
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/automaton'
      executable "${python_exe}"
      args = ['-B', 'createLevAutomata.py', '2', 'False', filePath("${buildDir}/moman/finenight/python")]
    }
  }
  
  dependsOn installMoman
  finalizedBy fixCRLFAutomaton
}

task createPackedIntSources {
  group = 'Build Regenerate'
  description = "Regenerates PackedInt src files."
  
  doLast {
    exec {
      workingDir 'src/java/org/apache/lucene/util/packed'
      executable "${python_exe}"
      args = ['-B', 'gen_BulkOperation.py']
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/packed'
      executable "${python_exe}"
      args = ['-B', 'gen_Direct.py']
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/packed'
      executable "${python_exe}"
      args = ['-B', 'gen_Packed64SingleBlock.py']
    }
    exec {
      workingDir 'src/java/org/apache/lucene/util/packed'
      executable "${python_exe}"
      args = ['-B', 'gen_PackedThreeBlocks.py']
    }
  }
}

task fixCRLFPacked(type: org.apache.lucene.gradle.FixCRLF) {
  sourceDir = "src/java/org/apache/lucene/util/packed"
  includes = "BulkOperation*.java,Direct*.java,Packed64SingleBlock.java,Packed*ThreeBlocks.py"
  encoding = "UTF-8"
  finalizedBy fixCRLFPacked
}

task runJflex(type: org.apache.lucene.gradle.JFlex) {
  group = 'Build Regenerate'
  description = "Runs jflex for StandardTokenizerImpl."
  
  inputDir = file("src/java/org/apache/lucene/analysis/standard")
  fileName = "StandardTokenizerImpl"
  disableBufferExpansion = true
  target = file("src/java/org/apache/lucene/analysis/standard")
}

task cleanJflex() {
  group = 'Build Regenerate'
  description = "Removes jflex generated artifacts."
  
  doLast {
    ant.delete(failonerror: 'false') {
      ant.fileset(dir: file("src/java/org/apache/lucene/analysis/standard"), includes: "**/*.java") {
        ant.containsregexp(expression: "generated.*by.*JFlex")
      }
    }
  }
}

task regenerate {
  group = 'Build Regenerate'
  description = "Regenerates various generated src files, automoton, packedints, jflex, javacc, etc"
  dependsOn cleanJflex, runJflex, createLevAutomaton, createPackedIntSources
}

